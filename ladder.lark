?start: rung

?rung: logic_expr out_sequence    -> standard_rung
     | "END"                      -> end_rung

// --- 条件式 ---
?logic_expr: logic_or
?logic_or: logic_and ( "OR" logic_and )*
?logic_and: logic_not ( "AND" logic_not )*
?logic_not: "NOT" factor          -> op_not
          | factor
?factor: "[" logic_expr "]"       -> nested
       | comparison                // D0 < 100 などを評価可能とするため
       | DEVICE                   -> device
       | "TRUE"                   -> const_true
       | "FALSE"                  -> const_false

?comparison: (DEVICE | NUMBER) COMP_OP (DEVICE | NUMBER) -> op_compare
COMP_OP: "==" | "!=" | ">=" | "<=" | ">" | "<"

// --- 出力 ---
// 出力が続く限り解析する
?out_sequence: "--(" out_target ")" out_sequence?

?out_target: DEVICE                    -> coil
           | "RES" DEVICE              -> res_inst
           | INST DEVICE NUMBER        -> timer_counter_inst
           | calc_expr                 -> calc_inst

// --- 演算 ---
//?calc_expr: DEVICE "=" DEVICE OP DEVICE -> op_math
//          | DEVICE "=" DEVICE           -> op_mov
?calc_expr: DEVICE "=" math_expr
?math_expr: term (OP term)*
?term: DEVICE | NUMBER


// --- トークン ---
DEVICE: /[XYMTCID]\d+/
INST: "TON" | "TOF" | "CTU"
OP: "+" | "-" | "*" | "/"
NUMBER: /\d+/

// あらゆる場所の空白（改行、タブ、スペース）を自動的に無視する
%import common.WS
%ignore WS